# Use the following variables to compile and link against ChoreonoidROS:
# CHOREONOID_ROS_FOUND                 - True if ChoreonoidROS was found on your system
# CHOREONOID_ROS_ROOT_DIR              - The base directory of ChoreonoidROS
# CHOREONOID_ROS_INCLUDE_DIRS          - List of directories of ChoreonoidROS and it's dependencies
# CHOREONOID_ROS_LIBRARY_DIRS          - List of directories of ChoreonoidROS and it's dependencies
# CHOREONOID_ROS_PLUGIN_LIBRARIES      - List of libraries to use the CnoidROSPlugin libary

set(CHOREONOID_ROS_ROOT_DIR @CMAKE_INSTALL_PREFIX@)

if(NOT CMAKE_VERSION VERSION_LESS 3.7.0)
  if(CMAKE_INSTALL_PREFIX_INITIALIZED_TO_DEFAULT)
    set(CMAKE_INSTALL_PREFIX "@CMAKE_INSTALL_PREFIX@" CACHE PATH "Install path prefix, prepended onto install directories." FORCE)
  endif()
endif()

set(CHOREONOID_ROS_LIB_DIR "@CMAKE_INSTALL_PREFIX@/@CHOREONOID_LIB_SUBDIR@")
set(CHOREONOID_ROS_PLUGIN_DIR "@CMAKE_INSTALL_PREFIX@/@CHOREONOID_PLUGIN_SUBDIR@")
set(CHOREONOID_ROS_SHARE_DIR "@CMAKE_INSTALL_PREFIX@/@CHOREONOID_SHARE_SUBDIR@")
set(CHOREONOID_ROS_CMAKE_CONFIG_SUBDIR "@CHOREONOID_ROS_CMAKE_CONFIG_SUBDIR@")

set(CHOREONOID_ROS_INCLUDE_DIRS "@include_dirs@")
set(CHOREONOID_ROS_LIBRARY_DIRS "@library_dirs@")

add_library(Choreonoid::CnoidROSPlugin SHARED IMPORTED GLOBAL)
if(WIN32)
  set_target_properties(Choreonoid::CnoidROSPlugin PROPERTIES
    IMPORTED_LOCATION ${CHOREONOID_ROS_PLUGIN_DIR}/CnoidROSPlugin.dll
    IMPORTED_IMPLIB ${CHOREONOID_ROS_LIB_DIR}/CnoidROSPlugin.lib
    IMPORTED_LOCATION_DEBUG ${CHOREONOID_ROS_PLUGIN_DIR}/CnoidROSPlugind.dll
    IMPORTED_IMPLIB_DEBUG ${CHOREONOID_ROS_LIB_DIR}/CnoidROSPlugind.lib
    IMPORTED_CONFIGURATIONS "RELEASE;DEBUG")
else()
  set_target_properties(Choreonoid::CnoidROSPlugin PROPERTIES
    IMPORTED_LOCATION ${CHOREONOID_ROS_PLUGIN_DIR}/libCnoidROSPlugin.so)
endif()
target_link_libraries(Choreonoid::CnoidROSPlugin INTERFACE Choreonoid::CnoidBodyPlugin)
set(CHOREONOID_ROS_PLUGIN_LIBRARIES Choreonoid::CnoidROSPlugin)

file(GLOB extmodules LIST_DIRECTORIES false "${CMAKE_CURRENT_LIST_DIR}/ext/*.cmake")
foreach(module ${extmodules})
  include(${module})
endforeach()
